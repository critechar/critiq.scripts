# Maintainer: Florian Walch <florian+aur@fwalch.com>
# Contributor: Florian Hahn <flo@fhahn.com>
# Neovim logo by Jason Long (see the svg file for links)

pkgname=neovim-git
pkgver=0.r3499.dac8bcd
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs.'
arch=('i686' 'x86_64')
url='http://neovim.io'
license=('custom:neovim')
depends=('jemalloc' 'libtermkey-bzr' 'libuv' 'libvterm-git-neovim' 'msgpack-c' 'unibilium')
makedepends=('cmake' 'git' 'luajit' 'lua51-messagepack' 'lua51-lpeg')
optdepends=('python2-neovim: for Python 2 plugin support (see :help nvim-python)'
            'python-neovim: for Python 3 plugin support (see :help nvim-python)'
            'xclip: for clipboard support (or xsel) (see :help nvim-clipboard)'
            'xsel: for clipboard support (or xclip) (see :help nvim-clipboard)')
source=("${pkgname}::git+https://github.com/neovim/neovim.git"
        nvim.desktop nvim.svg)
sha256sums=('SKIP'
            '0230460a2107ccc1e6b48fcc38f6b4d80fc8403d56e485d9a2db6e9a7c05fd78'
            '53b37cd374338237ec051d702bf3f35473d448adfea66ee216f12fffaaf14391')
provides=('neovim')
conflicts=('neovim')
install=neovim-git.install

pkgver() {
  cd "${pkgname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  mkdir -p "${pkgname}/build"
  cd "${pkgname}/build"
  cmake -G 'Unix Makefiles' -DCMAKE_BUILD_TYPE=RelWithDebInfo -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

check() {
  cd "${pkgname}/build"
  ./bin/nvim --version
  VIMRUNTIME=./runtime ./bin/nvim -u NONE -c ':quit'
}

package() {
  cd "${pkgname}/build"
  make DESTDIR="${pkgdir}" install
  cd ..
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  mkdir -p "${pkgdir}/usr/share/man/man1"
  gzip -c man/nvim.1 > ${pkgdir}/usr/share/man/man1/nvim.1.gz
  gzip -c man/nvimtutor.1 > ${pkgdir}/usr/share/man/man1/nvimtutor.1.gz
  install -Dm644 "${startdir}/nvim.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/nvim.svg"
  install -Dm644 "${startdir}/nvim.desktop" "${pkgdir}/usr/share/applications/nvim.desktop"
}

# vim:set sw=2 sts=2 et:
