# Maintainer: Florian Walch <florian+aur@fwalch.com>
# Contributor: Florian Hahn <flo@fhahn.com>

pkgname=neovim-git
pkgdesc="Vim's rebirth for the 21st century"
license=('custom:neovim')
pkgver=0.r2952.da457a1
pkgrel=1
depends=('libuv' 'unibilium' 'msgpack-git-neovim' 'libtermkey-git-neovim' 'libvterm-git-neovim')
makedepends=('cmake' 'git' 'unzip' 'luajit' 'lua' 'lua-messagepack' 'lua-bitop' 'lua-lpeg')
provides=('neovim')
arch=('i686' 'x86_64')
url=('https://github.com/neovim/neovim')
source=("$pkgname::git+https://github.com/neovim/neovim.git"
        nvim.desktop nvim.svg r_a_pr_python.vim.patch)
md5sums=('SKIP'
         '065e37e7d3b111850f8b683554fced97'
         'f3bbb62a08ae4f4fe1d253c23afffa75'
         '2c491b0273b25fbebd5cf1bfcecc7a42')
install=neovim-git.install

pkgver() {
  cd "${pkgname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  cmake -G 'Unix Makefiles' -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr
  patch "runtime/autoload/provider/python.vim" "${startdir}/r_a_pr_python.vim.patch"
  make
}

package() {
  cd "${pkgname}"
  DESTDIR="${pkgdir}" make install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${startdir}/nvim.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/nvim.svg"
  install -Dm644 "${startdir}/nvim.desktop" "${pkgdir}/usr/share/applications/nvim.desktop"
}

# vim:set sw=2 sts=2 et:
