# Maintainer: Florian Walch <florian+aur@fwalch.com>
# Contributor: Florian Hahn <flo@fhahn.com>
# Neovim logo by Jason Long (see the svg file for links)

pkgname=neovim-git
pkgver=0.r3289.04e098f
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs.'
arch=('i686' 'x86_64')
url='https://neovim.org'
license=('custom:neovim')
depends=('libuv' 'unibilium' 'msgpack-c' 'libtermkey-bzr' 'libvterm-git-neovim')
makedepends=('cmake' 'git' 'unzip' 'luajit' 'lua' 'lua-messagepack' 'lua-bitop' 'lua-lpeg')
optdepends=('python2-neovim: for Python plugin support (see :help nvim-python)'
            'xclip: for clipboard support (or xsel) (see :help nvim-clipboard)'
            'xsel: for clipboard support (or xclip) (see :help nvim-clipboard)'
           )
source=("${pkgname}::git+https://github.com/neovim/neovim.git"
        nvim.desktop nvim.svg)
md5sums=('SKIP'
         '065e37e7d3b111850f8b683554fced97'
         'f3bbb62a08ae4f4fe1d253c23afffa75')
provides=('neovim')
conflicts=('neovim')
install=neovim-git.install

pkgver() {
  cd "${pkgname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  cmake -G 'Unix Makefiles' -DCMAKE_BUILD_TYPE=RelWithDebInfo -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_JEMALLOC=false
  make
}

check() {
  cd "${pkgname}"
  ./bin/nvim --version
  VIMRUNTIME=./runtime ./bin/nvim -u NONE -c ':quit'
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${startdir}/nvim.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/nvim.svg"
  install -Dm644 "${startdir}/nvim.desktop" "${pkgdir}/usr/share/applications/nvim.desktop"
}

# vim:set sw=2 sts=2 et:
