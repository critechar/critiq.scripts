" --- Plug Scripts {{{
" Installation:
"   mkdir -p ~/.nvim/autoload
"   curl -fLo ~/.nvim/autoload/plug.vim \
"     https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
call plug#begin('~/.nvim/plugged')

Plug 'mhinz/vim-startify'
Plug 'bling/vim-airline'

Plug 'kana/vim-smartinput'
Plug 'kana/vim-textobj-user'
Plug 'kana/vim-smartword'
Plug 'godlygeek/tabular'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-surround' "csXX, dsX, ysMX, yssX
"Plug 'tpope/vim-abolish' "Abbreviation, substitution, coercion
Plug 'tpope/vim-repeat' "tpope plugin repeat
Plug 'tpope/vim-obsession'

Plug 'Shougo/unite.vim' , { 'on': ['Unite'] } "has many more commands
Plug 'rking/ag.vim' , { 'on': ['Ag', 'AgBuffer', 'AgFile'] } "AgAdd AgFromSearch

Plug 'powerman/vim-plugin-viewdoc'
Plug 'mbbill/undotree', { 'on': 'UndotreeToggle' }
Plug 'benekastah/neomake'
"Plug 'kassio/neoterm'
Plug 'christoomey/vim-tmux-navigator'

Plug 'rust-lang/rust.vim'
Plug 'PotatoesMaster/i3-vim-syntax'
Plug 'zaiste/tmux.vim'

"Plug 'critiqjo/vim-autoclose'
Plug 'critiqjo/lldb.nvim'
Plug 'critiqjo/vsearch.vim'

call plug#end()
" --- Plug Scripts }}}

filetype plugin indent on
if $TERM == 'linux'
  colorscheme desert
else
  colorscheme desertEx-v2
endif
syntax enable

" --- Global Options {{{
" -- UI
set diffopt+=vertical
set display+=lastline " Show as much of last line as possible
set foldmethod=marker
set hidden " Buffers can be hidden keeping its changes
set laststatus=2
set lazyredraw " Faster <C-O> mappings in insert mode with airline
set list
set listchars=tab:»\ ,trail:·
set number
set relativenumber " Relative line numbering
set ruler " Show line and column numbers
set scrolloff=1
set showcmd
set noshowmode
set splitright
set wildmenu " Enhanced command line suggestions
set wildmode=longest,full

" -- Usability
set autoindent
set backspace=indent,eol,start
set incsearch " Show results simultaneously while typing a search command
set mouse=
set nrformats-=octal " For use with <C-A> and <C-X>
set timeoutlen=1200 " <leader> timeout

" -- History management
set directory=~/.nvim/swps
set history=256
set undofile
set undodir=~/.nvim/undos
set undolevels=256  " Maximum number of changes that can be undone
set undoreload=2048 " Maximum number lines to save for undo on a buffer reload

" -- Formatting
set expandtab
set shiftwidth=4
set tabstop=4

" -- Session-saves
set ssop-=options " Do not store global and local options

" -- Spellcheck
set spellfile=~/.nvim/spell/en.utf-8.add
set spelllang=en
" --- Global Options }}}

" --- Plugin Options {{{
if $TERM != 'linux'
  let g:airline_powerline_fonts = 1
endif
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#whitespace#mixed_indent_algo = 1
let g:airline_theme = 'bubblegum'
"alttheme: 'powerlineish' "hi VertSplit ctermfg=233 ctermbg=233 cterm=NONE

let g:autoclose_no_squote = ['rust']
let g:autoclose_no_dquote = ['vim']
let g:surround_no_insert_mappings = 1
let g:viewdoc_openempty = 0
let g:tmux_navigator_no_mappings = 1

let g:neoterm_position = 'vertical'
let g:neoterm_automap_keys = '<leader>tt'
nnoremap <silent> <leader>tc :call neoterm#close_all()<CR>

call textobj#user#plugin('assignment', {
\   'lhs': {
\     'select-function': 'AssLHS',
\     'select': ['il', 'al'],
\   },
\   'rhs': {
\     'select-function': 'AssRHS',
\     'select': ['ir', 'ar'],
\   },
\ })
function! AssLHS()
  normal! ^
  let head_pos = getpos('.')
  normal! f=ge
  if getpos('.') == head_pos
    " entire line
    normal! g_
  endif
  let tail_pos = getpos('.')
  return ['v', head_pos, tail_pos]
endfunction
function! AssRHS()
  normal! g_
  let tail_pos = getpos('.')
  if getline('.')[tail_pos[2]-1] == ';'
    let tail_pos[2] -= 1
  endif
  normal! F=w
  let head_pos = getpos('.')
  return ['v', head_pos, tail_pos]
endfunction

map w <Plug>(smartword-w)
map b <Plug>(smartword-b)
map e <Plug>(smartword-e)
map ge <Plug>(smartword-ge)

nmap <M-b> <Plug>LLBreakSwitch
" --- Plugin Options }}}

" --- Keymaps {{{
" URL/XML/CString En/Decode maps: [X ]X where X=[uxy]
" CScope maps: <C-\>X <C-@>X <C-@><C-@>X where X=[cdefgist]

" Switch to normal mode
inoremap zj <Esc>
vnoremap zj <Esc>
tnoremap zj <C-\><C-N>

" Don't go to Ex mode, open cmd-line window instead.
nnoremap Q q:
nnoremap Y y$
nnoremap ]c :cn<CR>
nnoremap [c :cp<CR>
nnoremap <expr> 0 (getcurpos()[2]==1 ? "_" : "0")
nnoremap <expr> $ (getcurpos()[4]>1.0E9 ? "g_" : "$")
" Make ZQ redoable!
nnoremap ZQ :call SmartForceQuit()<CR>

nnoremap <M-/> :Unite line:buffers<CR>i
nnoremap <silent> <M-h> :TmuxNavigateLeft<cr>
nnoremap <silent> <M-j> :TmuxNavigateDown<cr>
nnoremap <silent> <M-k> :TmuxNavigateUp<cr>
nnoremap <silent> <M-l> :TmuxNavigateRight<cr>
nnoremap <silent> <c-w><c-w> :TmuxNavigatePrevious<cr>

inoremap <C-A> <C-O>^
inoremap <C-E> <C-O>$
" Make <C-U> and <CR> undoable
inoremap <C-U> <C-G>u<C-U>
inoremap <CR> <C-G>u<CR>

" C-mode: <C-B> Home; <C-E> End; <C-F> C-window; <C-H> backspace
"         <C-C> C-exit; <C-D> list-opts; <C-U> clear-back; <C-W> del-word
cnoremap <C-A> <Home>
" idea from tpope/vim-rsi
cnoremap <expr> <C-D> getcmdpos()>strlen(getcmdline()) ? "<C-D>" : "<Del>"
" mimic line kill, or if at the end of line -- up
cnoremap <expr> <C-K> getcmdpos()>strlen(getcmdline()) ? "<C-K>" :
        \ getcmdpos()<2 ? "<C-E><C-U>" : "<C-\>egetcmdline()[0:getcmdpos()-2]<CR>"

" --- Meta-keymaps mode-ict {{{
inoremap <M-b> <C-O>b
inoremap <M-B> <C-O>B
inoremap <M-d> <C-O>d
inoremap <M-e> <C-O>e<Right>
inoremap <M-E> <C-O>E<Right>
inoremap <M-f> <Esc>f
inoremap <M-F> <Esc>F
inoremap <M-o> <Esc>o
inoremap <M-O> <Esc>O
inoremap <M-t> <Esc>t
inoremap <M-T> <Esc>T
inoremap <M-u> <C-O>u
inoremap <M-w> <C-O>w
inoremap <M-W> <C-O>W
noremap! <M-h> <Left>
noremap! <M-j> <Down>
noremap! <M-k> <Up>
noremap! <M-l> <Right>
noremap! <M-x> <Del>
cnoremap <M-b> <S-Left>
cnoremap <M-e> <S-Right>
tnoremap <M-b> <Esc>b
tnoremap <M-d> <Esc>d
tnoremap <M-f> <Esc>f
tnoremap <M-F> <Esc>F
tnoremap <M-h> <Esc>h
tnoremap <M-j> <Esc>j
tnoremap <M-k> <Esc>k
tnoremap <M-l> <Esc>l
tnoremap <M-n> <Esc>n
tnoremap <M-p> <Esc>p
tnoremap <M-u> <Esc>u
tnoremap <M-w> <Esc>w
tnoremap <M-x> <Esc>x
" --- Meta-keymaps }}}

vnoremap <leader>y "+y
nnoremap <leader>y "+y
nnoremap <leader>Y "+y$
nnoremap <leader>p o<Esc>"+p
nnoremap <leader>P O<Esc>"+p

nnoremap <leader>b :term cd %:h; $SHELL<CR>
nnoremap <leader>cd :cd %:h<CR>
nnoremap <leader>cl :set invcursorline<CR>
nnoremap <leader>co :copen<CR>
nnoremap <leader>do :DiffOrig<CR>
nnoremap <leader>hl :set invhlsearch<CR>
nnoremap <leader>m :MouseToggle<CR>
nnoremap <leader>n :<C-U>b <C-R>=GetModifiableBuffer(v:count, 1)<CR><CR>
nnoremap <leader>N :<C-U>b <C-R>=GetModifiableBuffer(v:count, -1)<CR><CR>
nnoremap <leader>o :Startify<CR>
nnoremap <leader>q :call SmartBufferClose()<CR>
nnoremap <leader>Q :qall<CR>
nnoremap <leader>s :set invspell<CR>
nnoremap <leader>tq :tabc<CR>
"Neoterm maps: <leader>tt <leader>tc
nnoremap <leader>u :UndotreeToggle<CR>
nnoremap <leader>vo :call CloseHiddenBuffers()<CR>
nnoremap <leader>w :update<CR>
" --- Keymaps }}}

" --- Autocommands {{{
augroup critiq_ft
  autocmd!
  autocmd FileType netrw nnoremap <buffer> qq <C-^> | nmap <buffer> <Space> mf
  autocmd FileType qf nnoremap <buffer> qq <C-w>q | setlocal nonu nornu
  autocmd FileType rust setlocal makeprg=cargo\ build | Tmap cargo build
  autocmd FileType vim\|python setlocal ts=2 sw=2
  autocmd WinLeave,InsertEnter * setlocal nocursorline
  autocmd WinEnter,BufEnter,InsertLeave * setlocal cursorline
augroup END
" --- Autocommands }}}

" --- Custom commands {{{
command! MouseToggle if &mouse=="" | set mouse=a | else | set mouse= | endif

" See the diff between the current buffer and the file on disk.
command! DiffOrig vert new | set bt=nofile | r ++edit # | 0d_ | diffthis
                \ | wincmd p | diffthis
" --- Custom commands }}}

" vim:fdm=marker:ts=2:sw=2
