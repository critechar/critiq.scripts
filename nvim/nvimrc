"set nocompatible " Always nocompatible in neovim

" Plug Scripts +++ {{{
call plug#begin('~/.nvim/plugged')

Plug 'bling/vim-airline'
Plug 'tpope/vim-fugitive'
Plug 'powerman/vim-plugin-viewdoc'
Plug 'rking/ag.vim' , { 'on': ['Ag', 'AgBuffer', 'AgFile'] } "AgAdd AgFromSearch
Plug 'mbbill/undotree', { 'on': 'UndotreeToggle' }
"Plug 'Shougo/vimshell'
"Plug 'junegunn/vim-easy-align'

call plug#end()
" Plug Scripts --- }}}

filetype plugin indent on
colorscheme desertEx-v2
syntax enable

" Global Options +++ {{{
" -= UI =-
set colorcolumn=79
set display+=lastline " Show as much of last line as possible
set foldmethod=marker
set hidden " Buffers can be hidden keeping its changes
set laststatus=2
set number
set relativenumber " Relative line numbering
set ruler " Show line and column numbers
set scrolloff=1
set showcmd
set wildmenu " Enhanced command line suggestions
set wildmode=longest,full

let g:airline_powerline_fonts = 1
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#whitespace#mixed_indent_algo = 1
let g:airline_theme = 'bubblegum'
"let g:airline_theme = 'powerlineish'
"hi VertSplit ctermfg=233 ctermbg=233 cterm=NONE
"let g:airline_theme = 'zenburn'
let g:viewdoc_openempty = 0

" -= Usability =-
set autoindent
set backspace=indent,eol,start
set incsearch " Show results simultaneously while typing a search command
set nrformats-=octal " For use with <C-A> and <C-X>
set timeoutlen=2000 " <leader> will have 2000ms timeout

" -= History management =-
set directory=~/.nvim/swps
set history=256
set undofile
set undodir=~/.nvim/undos
set undolevels=256	" Maximum number of changes that can be undone
set undoreload=2048	" Maximum number lines to save for undo on a buffer reload

" -= Formatting =-
set shiftwidth=4
set tabstop=4

" -= Session-saves =- :help :mksession
set ssop-=options " Do not store global and local values

" -= Spellcheck =-
set spellfile=~/.nvim/spell/en.utf-8.add
set spelllang=en
" Global Options --- }}}

" Keymaps +++ {{{
" Don't go to Ex mode
map Q q:
map Y y$
noremap <A-n> :cn<CR>
noremap <A-p> :cp<CR>

inoremap <C-A> <C-O>^
inoremap <C-E> <C-O>$
inoremap <C-B> <S-Left>
inoremap <C-F> <S-Right>
" Make <C-U> undoable
inoremap <C-U> <C-G>u<C-U>
inoremap qj <Esc>

vnoremap qj <Esc>

" C-mode: <C-B> Home; <C-E> End; <C-F> C-window; <C-H> backspace
"		  <C-C> C-exit; <C-D> list-opts; <C-U> clear-back; <C-W> del-word
cnoremap <C-A> <Home>
" idea from tpope/vim-rsi
cnoremap <expr> <C-D> getcmdpos()>strlen(getcmdline())? "<C-D>": "<Del>"
cnoremap <C-B> <S-Left>
cnoremap <C-F> <S-Right>
cnoremap <expr> <C-K> getcmdpos()>strlen(getcmdline())? "<UP>": "<C-\>egetcmdline()[0 : getcmdpos()-2]<CR>"
cnoremap <C-J> <Down>
cnoremap <C-H> <Space><BS><Left>
cnoremap <C-L> <Space><BS><Right>

map <leader>p o<Esc>"+p
map <leader>P O<Esc>"+p
map <leader>y "+y
map <leader>Y "+y$
noremap <leader>vo :VisibleOnly<CR>
noremap <leader>cd :cd %:p:h<CR>
noremap <leader>do :DiffOrig<CR>
noremap <leader>h :HLToggle<CR>
noremap <leader>m :MouseToggle<CR>
noremap <leader>n :<C-U>exe 'bn ' . max([1,v:count])<CR>
noremap <leader>N :<C-U>exe 'bp ' . max([1,v:count])<CR>
noremap <leader>o :call RecentFiles(25)<CR>:<C-U>call OpenRecentFile()<left>
noremap <leader>q :bp<BAR>bd #<CR>
noremap <leader>Q :qall<CR>
noremap <leader>s :SpellToggle<CR>
noremap <leader>u :UndotreeToggle<CR>
noremap <leader>w :w<CR>
" Keymaps --- }}}

" Custom commands +++ {{{
command! SpellToggle if &spell | set nospell | else | set spell | endif

command! HLToggle if &hlsearch | set nohlsearch | else | set hlsearch | endif

if has('mouse')
	command! MouseToggle if &mouse=="" | set mouse=a
				\ | else | set mouse= | endif
endif

" See the diff between the current buffer and the file on disk.
command! DiffOrig vert new | set bt=nofile | r ++edit # | 0d_ | diffthis
				\ | wincmd p | diffthis

command! -nargs=* VisibleOnly call CloseHiddenBuffers()
" Custom commands --- }}}

" RecentFiles +++ {{{
function! RecentFiles(max)
	let i = 0
	let n = len(v:oldfiles)
	if a:max && n > a:max
		let n = a:max
	endif
	while i < n
		echo i ": " v:oldfiles[i]
		let i = i+1
	endw
endfunction

function! OpenRecentFile(i)
	e `=expand(v:oldfiles[a:i])`
endfunction
" RecentFiles --- }}}

" CloseHiddenBuffers +++ {{{
" src: http://stackoverflow.com/questions/2974192/
function! CloseHiddenBuffers()
	" figure out which buffers are visible in any tab
	let visible = {}
	for t in range(1, tabpagenr('$'))
		for b in tabpagebuflist(t)
			let visible[b] = 1
		endfor
	endfor
	" close any buffer that are loaded and not visible
	let l:tally = 0
	for b in range(1, bufnr('$'))
		if bufloaded(b) && !has_key(visible, b)
			let l:tally += 1
			exe 'bw ' . b
		endif
	endfor
	echon "Deleted " . l:tally . " buffers"
endfun
" CloseHiddenBuffers --- }}}

" CScope stuff +++ {{{
function! LoadCscope()
	let db = findfile("cscope.out", ".;")
	if (!empty(db))
		let path = strpart(db, 0, match(db, "/cscope.out$"))
		set nocscopeverbose " suppress 'duplicate connection' error
		exe "cs add " . db . " " . path
		set cscopeverbose
	endif
endfunction
au BufEnter /*.{c,h} call LoadCscope()
" CScope stuff --- }}}

"do patch /usr/share/nvim/runtime/autoload/provider/python.vim
